/***************************************************************
 * ARAL Parser                                                 *
 * Author: Markus Schordan, 2009.                              *
 * (C) Markus Schordan                                         *
 ***************************************************************/

%{

#include <stdio.h>
#include <stdlib.h>

#include "aralparser_decls.h"

char *input_filename = NULL;
char tempfilename[30];
char *output_filename = tempfilename;
Aral::Data* aralIrRoot;

//void process_unit(struct func *);

void MSG(char* s)
{
#ifdef TEST 
	printf("%s",s);
#endif
}

#ifndef NDEBUG
#ifndef YYDEBUG
#define YYDEBUG 1
#endif
#endif

%}

%union {
char* idstring; /* IDENT */
char* string; /* STRING */
long intval; /* INTEGER */
int length; /* #elements */
Data* araldata;
Set* aralset;
Map* aralmap;
MapList* aralmaplist;
List* arallist;
InfoElementList* aralinfoelementlist;
Tuple* araltuple;
MapPair* aralmappair;
LocationSpecifier* locationspecifier;
FlowSpecifier flowspecifier;
AnnotationData* annotdata;
AnnotationDataList* annotdatalist;
ResultSection* resultsection;
ResultSectionList* resultsectionlist;
char* sectionname;
AralFile* aralfile;
Type* araltype;
ComplexType* complexaraltype;
}

%right NOT
%left OR
%left AND
%left '=' NEQ
%left '<' '>' GT LT 
%left '+' '-'
%left '*' '/' '%'
%left '^'

%token ';' ',' 
%token '{' '}' '[' ']' '(' ')'
%token TOP BOT
%token <idstring> IDENT 
%token <intval> INTEGER IDCODE LABEL
%token <string> STRING
%token ANALYSIS MAPPING RESULT TYPE DATA END NAME
%token PROGRAM_LOCSPEC NOFLOW PRE POST FUNCTION_LOCSPEC FILE_LOCSPEC
%token DEFAULT RIGHTARROW CONFIG

%token TYPE_MAP MAPSEPARATOR
%token TYPE_LABEL TYPE_TUPLE TYPE_SET TYPE_LIST TYPE_LIFT 
%token TYPE_INT TYPE_FLOAT TYPE_STRING
%token TYPE_PROGID TYPE_FUNCID TYPE_STMTID TYPE_EXPRID TYPE_VARID
%token LT GT NEQ '='

%type <araldata> context data basic_element info_element constraint opt_config_section
%type <aralset> set set_elements opt_set_elements
%type <aralmaplist> opt_map_section map_list
%type <aralmap> mapdef map 
%type <aralmap> map_elements opt_map_elements opt_map_content
%type <arallist> list_elements opt_list_elements
%type <arallist> tuple_elements opt_tuple_elements
%type <aralinfoelementlist> info_element_list
%type <aralmappair> mapping_element
%type <locationspecifier> location_specifier
%type <flowspecifier> flow_specifier
%type <annotdata> annot_data
%type <annotdatalist> annot_data_list data_section
%type <resultsection> result_section
%type <resultsectionlist> opt_result_section_list result_section_list
%type <sectionname> name_section
%type <aralfile> aral_file;
%type <araltype> type_equation context_type type opt_type_list 
%type <complexaraltype> type_list

%start start

%%

start		: aral_file
		;
aral_file	: ANALYSIS opt_config_section opt_map_section opt_result_section_list END {aralIrRoot=new AralFile($2,$3,$4);}
		;

opt_config_section: /* eps */ { $$=0; }
		| CONFIG data { $$=$2; }		
		;

opt_map_section: /* eps */ { $$=new MapList(); }
		| MAPPING map_list { $$=$2; }
		;

map_list	: /* eps */ { $$=new MapList(); }
		| map_list mapdef  { $1->addBack($2);}
		;

mapdef		: TYPE_MAP '(' type ',' type ')' ':' map ';' {$$=$8;}
		;

opt_result_section_list: /* eps */ {$$=new ResultSectionList();}
		| result_section_list {$$=$1;}
		;

result_section_list: result_section { $$=new ResultSectionList(); $$->addFront($1); }
		| result_section_list result_section { $1->addBack($2); $$=$1; }
		;

result_section	: RESULT name_section type_section data_section END { $$=new ResultSection($2,0,$4); }
		;
name_section	: NAME IDENT {$$=$2;}
		;
type_section	: TYPE opt_type_decls

opt_type_decls	: /* eps */
		| type_decls
		;

type_decls	: type_equations context_type type
		| type_equations type
		| context_type type
		| type
		;

type_equations	: type_equation
		| type_equations type_equation
		;

type_equation	: IDENT '=' type ';' { $$=new ComplexType($1,$3); }
		;

context_type	: '<' type '>' { $$=new ComplexType("context",$2); }
		;

data_section	: DATA annot_data_list { $$=$2;}
		;

annot_data_list: /* eps */ { $$=new AnnotationDataList(); }
		| annot_data_list annot_data { $1->addBack($2); $$=$1; }
		;

annot_data: location_specifier flow_specifier info_element_list ';' { $$=new AnnotationData($1,$2,$3); }
		;

location_specifier: PROGRAM_LOCSPEC { $$=new LocationSpecifier(LocationSpecifier::E_PROGRAM_LOCSPEC);}
		| FILE_LOCSPEC '(' STRING ')' { $$=new LocationSpecifier(LocationSpecifier::E_FILE_LOCSPEC,std::string($3));}
		| FUNCTION_LOCSPEC '(' STRING ')' { $$=new LocationSpecifier(LocationSpecifier::E_FUNCTION_LOCSPEC,std::string($3));}
		| LABEL {$$=new LocationSpecifier(new Label($1));}
		;
flow_specifier	: NOFLOW { $$=Aral::E_NOFLOW;}
		| PRE {$$=Aral::E_PRE; }
		| POST {$$=Aral::E_POST; }
		;

/* build a InfoElementList */
info_element_list	: info_element { $$=new InfoElementList(); $$->addFront($1);}
		| info_element_list ',' info_element { $1->addBack($3); $$=$1;}
		;

info_element		: context data { $$=new InfoElement($1,$2); }
		;

context		: /* eps */ {$$=0;}
		| '<' data '>' {$$=$2;}
		;

data		: basic_element
		| '$' constraint '$' { $$=new Constraint($2); }
		| set {$$=$1;}
		| map {$$=$1;}
		| '[' opt_list_elements ']' { $$=$2; }
		| '(' opt_tuple_elements ')' 
		  { 
			Tuple* tupleobj=new Tuple($2->size());
			int pos=0;
			for(List::iterator i=$2->begin();i!=$2->end();++i,++pos) {
				Aral::Data* temp=(*i)->deepCopy();
				tupleobj->setAt(pos,temp);
			}
			delete $2;
			$$=tupleobj;
		  }
		;

set		: '{' opt_set_elements '}' { $$=$2; }
		;

map		: '{' DEFAULT RIGHTARROW data opt_map_content '}' 
		{ 
			assert($5);
			$$=$5; $$->setDefaultElement($4);
		}
		;

opt_map_content	: /* eps */ { $$ = new Map(); }
		| map_separator opt_map_elements { $$=$2; }
		;

map_separator	: MAPSEPARATOR
		| ','
		;

opt_map_elements: /* empty */
		{
			$$ = new Map();
		}
		| map_elements
		{
			$$=$1;
		}
opt_set_elements: /* empty */
		{
			$$ = new Set();
		}
		| set_elements
		{
			$$=$1;
		}
		;
opt_list_elements: /* empty */
		{
			$$ = new List();
		}
		| list_elements
		{
			$$=$1;
		}
		;
opt_tuple_elements: /* empty */
		{
			$$ = new List();
		}
		| tuple_elements
		{
			$$=$1;
		}
		;
map_elements	:
 		mapping_element
		{ 	
			$$ = new Map();
			$$ -> add($1);
		}
		| map_elements ',' mapping_element	
		{ 
			$1->add($3);
			$$=$1; 
		} 
    		;
mapping_element	: data RIGHTARROW data 
		{ 
			MapPair* tupleobj=new MapPair();
			tupleobj->setAt(0,$1); 
			tupleobj->setAt(1,$3);
			$$=tupleobj;
		}

set_elements:
 	data
	{ 	
		$$ = new Set();
		$$ -> add($1);
	}
	| set_elements ',' data	
	{ 
		$1->add($3);
		$$=$1; 
	} 
    	;

list_elements:
 	data
	{ 	
		$$ = new List();
		$$ -> addBack($1);
	}
	| list_elements ',' data	
	{ 
		$1->addBack($3);
		$$=$1; 
	} 
    	;

tuple_elements:
 	data
	{ 	
		$$ = new List();
		$$ -> addBack($1);
	}
	| tuple_elements ',' data	
	{ 
		$1->addBack($3);
		$$=$1; 
	} 
    	;

basic_element	: STRING { $$=new String($1); }
		| INTEGER { $$=new Int32($1); }
		| LABEL { $$=new Label($1); }
		| IDCODE { $$=new VarId($1); }
		| TOP { $$=new TopElement(); }
 		| BOT { $$=new BotElement(); }
		| IDENT { $$=new Ident($1); }
		;

type		: TYPE_LABEL { $$=new ComplexType("label"); }
		| TYPE_TUPLE '(' opt_type_list ')' { $$=new ComplexType("tuple",$3); }
		| TYPE_SET '(' type ')' { $$=new ComplexType("set",$3); }
		| TYPE_MAP '(' type ',' type ')' { ComplexType* ct=new ComplexType("map",$3); ct->push_back($5); $$=ct; }
		| TYPE_LIST '(' type ')' { $$=new ComplexType("list",$3); }
		| TYPE_LIFT '(' type ')' { $$=new ComplexType("lift",$3); }
		| TYPE_INT '(' INTEGER ')' { $$=new IntType(32); }
		| TYPE_FLOAT { $$=new ComplexType("float"); }
		| TYPE_STRING { $$=new ComplexType("string"); }
		| TYPE_PROGID { $$=new ComplexType("progid"); }
		| TYPE_FUNCID { $$=new ComplexType("funcid"); }
		| TYPE_STMTID { $$=new ComplexType("stmtid"); }
		| TYPE_EXPRID { $$=new ComplexType("exprid"); }
		| TYPE_VARID { $$=new ComplexType("varid"); }
		| IDENT { $$=new ComplexType("ident"); }
		;

opt_type_list	: /* eps */ { $$=new ComplexType("tuple",0); }
		| type_list { $$=$1; }
		;

type_list	: type { $$=new ComplexType("tuple",$1); }
		| type_list ',' type { $$=$1; $$->push_back($3); }
		;

constraint	: basic_element             { $$=$1; }
		| NOT constraint            { $$=new UnaryOperator("not",$2); }
		| constraint '<' constraint { $$=new BinaryOperator("<",$1,$3); }
		| constraint '>' constraint { $$=new BinaryOperator(">",$1,$3); }
		| constraint GT constraint  { $$=new BinaryOperator(">=",$1,$3); }
		| constraint LT constraint  { $$=new BinaryOperator("<=",$1,$3); }
		| constraint '=' constraint { $$=new BinaryOperator("=",$1,$3); }
		| constraint NEQ constraint { $$=new BinaryOperator("<>",$1,$3); }
		| constraint '+' constraint { $$=new BinaryOperator("+",$1,$3); }
		| constraint '-' constraint { $$=new BinaryOperator("-",$1,$3); }
		| constraint '/' constraint { $$=new BinaryOperator("/",$1,$3); }
		| constraint '*' constraint { $$=new BinaryOperator("*",$1,$3); }
		| constraint '%' constraint { $$=new BinaryOperator("%",$1,$3); }
		| constraint '^' constraint { $$=new BinaryOperator("^",$1,$3); }
		| constraint AND constraint { $$=new BinaryOperator("and",$1,$3); }
		| constraint OR  constraint { $$=new BinaryOperator("or",$1,$3); }
		| '(' constraint ')'        { $$=$2; } 
		;
%%

void
aralerror(const char* errorMessage)
{
  /* arallineno, errorMessage, araltext */
  fprintf(stderr,"ARAL parse error: line %d: %s at %s\n",arallineno,errorMessage,araltext);
  exit(2);
}

#if 0
void process_unit(struct func *unit)
{
  /* build symboltables for all functions */
  build_symtab(unit);

  /********************************************/
  /* insert analysis and optimizations here!! */
  /********************************************/

  /* output unit */
  output_unit(output_filename,unit); 
}
#endif

